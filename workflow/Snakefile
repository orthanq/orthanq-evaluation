#set the environment variable to conda library folder to prevent libhdf5 not being found by snakemake
LD_LIBRARY_PATH = "$CONDA_PREFIX/lib"
shell.prefix("""
set -euo pipefail;
export LD_LIBRARY_PATH=\"{LD_LIBRARY_PATH}\";
""".format(LD_LIBRARY_PATH=LD_LIBRARY_PATH))

configfile: "config/config.yaml"

ruleorder: orthanq_candidates > orthanq_call

report: "report/workflow.rst"

if config["simulation"] and config["subclonal_sample_creation"] == False:

    include: "rules/common.smk"
    include: "rules/simulation.smk"
    include: "rules/candidate_generation.smk"
    include: "rules/alignment.smk"
    include: "rules/calling.smk"

    rule all:
        input:
            [
                f"results/fractions/{row['sample_name']}-{row['hla']}-{row['num_reads']}_1.fq"
                for _, row in simulated_sample.iterrows()
            ],
            expand("results/orthanq-candidates/{hla}.vcf.gz.tbi", hla=loci),
            expand(
                "results/calls/{sample}_{hla}.bcf",
                sample=simulated_sample.sample_name.unique(),
                hla=loci,
            ),
            expand(
                "results/orthanq/{sample}_{hla}/{sample}_{hla}.csv",
                sample=simulated_sample.sample_name.unique(),
                hla=loci,
            ),
            # todo: configure the workflow to target simulated samples as well
            # "results/orthanq/final_report.csv"

elif config["simulation"] == False and config["subclonal_sample_creation"] == False:
    include: "rules/common.smk"
    include: "rules/candidate_generation.smk"
    include: "rules/alignment.smk"
    include: "rules/calling.smk"
    include: "rules/evaluation.smk"

    rule all:
        input:
            expand("results/arcasHLA/{sample}_{hla}/{sample}_mapped.genotype.json",
            sample=samples.sample_name,
            hla=loci
            ),
            expand("results/optitype/{sample}_result.tsv",sample=samples.sample_name),
            expand("results/HLA-LA/{sample}/hla/R1_bestguess_G.txt",sample=samples.sample_name),
            expand("results/vg/alignment/{sample}_vg.bam",sample=samples.sample_name),
            expand("results/orthanq/{sample}_{hla}/{sample}_{hla}.csv",sample=samples.sample_name,
                hla=loci,
            ),
            expand("results/orthanq/{sample}_{hla}/3_field_solutions.html",sample=samples.sample_name,
                hla=loci
            ),
            ["results/vega/runtimes.html", "results/vega/tp_fp_plot.html", "results/vega/evaluation.html"],
            # "results/vega/tp_fp_plot.html",
            # "results/orthanq/final_report.csv"
            "results/validation/validation.tsv",
            "results/datavzrd-report/sample_sheet", 
            "results/datavzrd-report/runtimes",
            "results/datavzrd-report/orthanq",
            "results/datavzrd-report/optitype",
            "results/datavzrd-report/arcashla",
            "results/datavzrd-report/hla_la",
            "results/datavzrd-report/accuracy_comparison",

elif config["subclonal_sample_creation"]:
    include: "rules/common.smk"
    include: "rules/simulation.smk"
    include: "rules/candidate_generation.smk"
    include: "rules/alignment.smk"
    include: "rules/calling.smk"
    include: "rules/clonal_sample_creation.smk"

    rule all:
        input:
            "results/mixed/subclonal_sample_1.fq",
            expand("results/orthanq/subclonal_{hla}/subclonal_{hla}.csv", hla=loci)
